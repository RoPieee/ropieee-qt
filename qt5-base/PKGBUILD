# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -no-reduce-relocations -- not available on ARM
#  - explicitly disable x86 optimizations
#  - use OpenGLES 2.0

pkgbase=qt5-base
pkgname=(qt5-base-ropieee qt5-xcb-private-headers-ropieee)
_qtver=5.12.6
pkgver=${_qtver/-/}
provides=("qt5-base=${pkgver}")
conflicts=('qt5-base')
pkgrel=1
arch=('armv7h')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework'
depends=('libjpeg-turbo' 'xcb-util-keysyms' 'xcb-util-renderutil' 'libgl' 'fontconfig' 'xdg-utils'
         'xcb-util-wm' 'libxrender' 'libxi' 'sqlite' 'xcb-util-image' 'icu' 'pcre2'
         'tslib' 'libinput' 'libsm' 'libxkbcommon-x11' 'libproxy' 'libcups' 'openssl-1.0' 'double-conversion')
makedepends=('libfbclient' 'libmariadbclient' 'sqlite' 'unixodbc' 'postgresql-libs' 'alsa-lib' 'gst-plugins-base-libs'
             'gtk3' 'libpulse' 'cups' 'freetds' 'fakeroot')
optdepends=('qt5-svg: to use SVG icon themes'
            'postgresql-libs: PostgreSQL driver'
            'libmariadbclient: MariaDB driver'
            'unixodbc: ODBC driver'
            'libfbclient: Firebird/iBase driver'
            'freetds: MS SQL driver'
            'gtk3: GTK platform plugin')
conflicts=('qtchooser')
groups=('qt' 'qt5')
_pkgfqn="${pkgbase/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
	"glibc-2.28-NEW.patch")
sha256sums=('6ab52649d74d7c1728cf4a6cf335d1142b3bf617d476e2857eb7961ef43f9f27'
            '77868eb1124a6f524873493573c091eb11366ab0b4f5568a47d30b1ac92b2396')

prepare() {
  cd ${_pkgfqn}

  # Work around build failure with glibc 2.28
  #patch -p1 -i ../glibc-2.28-NEW.patch

  # Build qmake using Arch {C,LD}FLAGS
  # This also sets default {C,CXX,LD}FLAGS for projects built using qmake
  sed -i -e "s|^\(QMAKE_CFLAGS_RELEASE.*\)|\1 ${CFLAGS}|" \
    mkspecs/common/gcc-base.conf
  sed -i -e "s|^\(QMAKE_LFLAGS_RELEASE.*\)|\1 ${LDFLAGS}|" \
    mkspecs/common/g++-unix.conf
}

build() {
  cd ${_pkgfqn}

  export PKG_CONFIG_SYSROOT_DIR=/
  export PKG_CONFIG_LIBDIR=/usr/lib/pkgconfig:/usr/share/pkgconfig:/opt/vc/lib/pkgconfig

  ./configure -confirm-license -opensource -v \
    -force-pkg-config \
    -sysroot / \
    -prefix /usr \
    -docdir /usr/share/doc/qt \
    -headerdir /usr/include/qt \
    -archdatadir /usr/lib/qt \
    -datadir /usr/share/qt \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt/examples \
    -nomake examples \
    -nomake tests \
    -nomake tools \
    -no-rpath \
    -no-pch \
    -no-glib \
    -no-gbm \
    -no-gtk \
    -no-xcb \
    -skip qt3d \
    -skip qtactiveqt \
    -skip qtandroidextras \
    -skip qtcanvas3d \
    -skip qtconnectivity \
    -skip qtdatavis3d \
    -skip qtdoc \
    -skip qtgamepad \
    -reduce-exports \
    -system-harfbuzz \
    -system-freetype \
    -fontconfig \
    -no-use-gold-linker \
    -no-reduce-relocations \
    -no-sse2 \
    -no-sse3 \
    -no-ssse3 \
    -no-sse4.1 \
    -no-sse4.2 \
    -no-avx \
    -no-avx2 \
    -no-feature-getentropy \
    -opengl es2 \
    -eglfs \
    -device linux-rasp-pi2-g++ \
    -device-option CROSS_COMPILE=/usr/bin/
#    -no-feature-statx \
#    -no-feature-renameat2 \

  make
}

package_qt5-base-ropieee() {
  pkgdesc='A cross-platform application and UI framework'
  provides=("qt5-base=${pkgver}")
  conflicts=('qt5-base')
  install=${pkgname}.install

  cd ${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  install -D -m644 LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in pri file
  sed -i "s|${srcdir}/${_pkgfqn}|/usr|" \
    "${pkgdir}"/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/usr/bin/*; do
    ln -s /usr/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done
}

package_qt5-xcb-private-headers-ropieee() {
  pkgdesc='Private headers for Qt5 Xcb'
  depends=("qt5-base-raspberrypi=$pkgver")
  provides=("qt5-xcb-private-headers=${pkgver}")
  conflicts=('qt5-xcb-private-headers')
  optdepends=()
  groups=()
  conflicts=()

  cd ${_pkgfqn}
  install -d -m755 "$pkgdir"/usr/include/qtxcb-private
  cp -r src/plugins/platforms/xcb/*.h "$pkgdir"/usr/include/qtxcb-private/
}
