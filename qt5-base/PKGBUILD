# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -no-reduce-relocations -- not available on ARM
#  - explicitly disable x86 optimizations
#  - use OpenGLES 2.0

pkgbase=qt5-base
pkgname=(qt5-base-ropieee qt5-xcb-private-headers-ropieee)
_qtver=5.12.7
pkgver=${_qtver/-/}
provides=("qt5-base=${pkgver}")
conflicts=('qt5-base')
pkgrel=1
arch=('armv7h')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework'
depends=('pkgconfig' 'libjpeg-turbo' 'xcb-util-keysyms' 'xcb-util-renderutil' 'fontconfig' 'xdg-utils'
         'shared-mime-info' 'xcb-util-wm' 'libxrender' 'libxi' 'sqlite' 'xcb-util-image'
         'tslib' 'libinput' 'libxkbcommon-x11' 'libproxy' 'double-conversion' 'md4c')
makedepends=('sqlite' 'alsa-lib' 'gst-plugins-base-libs'
             'libpulse' 'vulkan-headers')
optdepends=('qt5-svg: to use SVG icon themes'
            'qt5-wayland: to run Qt applications in a Wayland session'
            'qt5-translations: for some native UI translations'
            'postgresql-libs: PostgreSQL driver'
            'mariadb-libs: MariaDB driver'
            'unixodbc: ODBC driver'
            'libfbclient: Firebird/iBase driver'
            'freetds: MS SQL driver'
            'gtk3: GTK platform plugin')
conflicts=('qtchooser')
groups=('qt' 'qt5')
_pkgfqn="${pkgbase/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        'qt5-base-cflags.patch')
sha256sums=('b18939cb25d90aef8721fb12ec34c3632d3490ced958e41f6c7a52064643665d'
            '3d6524381b2bbd35c9d85fe6d9f3e39be645e12658a810604dfe981633f78d8b')

prepare() {
  cd ${_pkgfqn}

# Build qmake using Arch {C,LD}FLAGS
# This also sets default {C,CXX,LD}FLAGS for projects built using qmake
sed -i -e "s|^\(QMAKE_CFLAGS_RELEASE.*\)|\1 ${CFLAGS}|" \
    mkspecs/common/gcc-base.conf
sed -i -e "s|^\(QMAKE_LFLAGS_RELEASE.*\)|\1 ${LDFLAGS}|" \
    mkspecs/common/g++-unix.conf

  #patch -p1 -i ../qt5-base-cflags.patch # Use system CFLAGS
}

build() {
  cd ${_pkgfqn}

  export PKG_CONFIG_EXECUTABLE=/usr/bin/pkg-config
  export PKG_CONFIG_SYSROOT_DIR=/
  export PKG_CONFIG_LIBDIR=/usr/lib/pkgconfig:/usr/share/pkgconfig:/opt/vc/lib/pkgconfig

  ./configure -confirm-license -opensource -v -make libs \
    -force-pkg-config \
    -release \
    -sysroot / \
    -prefix /usr \
    -docdir /usr/share/doc/qt \
    -headerdir /usr/include/qt \
    -archdatadir /usr/lib/qt \
    -datadir /usr/share/qt \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt/examples \
    -openssl-linked \
    -nomake examples \
    -nomake tests \
    -nomake tools \
    -no-rpath \
    -no-pch \
    -no-glib \
    -no-gbm \
    -no-gtk \
    -no-xcb \
    -skip qt3d \
    -skip qtactiveqt \
    -skip qtandroidextras \
    -skip qtcanvas3d \
    -skip qtconnectivity \
    -skip qtdatavis3d \
    -skip qtdoc \
    -skip qtgamepad \
    -optimized-qmake \
    -reduce-exports \
    -system-harfbuzz \
    -system-freetype \
    -fontconfig \
    -journald \
    -no-use-gold-linker \
    -no-reduce-relocations \
    -no-sse2 \
    -no-sse3 \
    -no-ssse3 \
    -no-sse4.1 \
    -no-sse4.2 \
    -no-avx \
    -no-avx2 \
    -no-feature-getentropy \
    -opengl es2 \
    -eglfs \
    -device linux-rasp-pi2-g++ \
    -device-option CROSS_COMPILE=/usr/bin/

  make
}

package_qt5-base-ropieee() {
  pkgdesc='A cross-platform application and UI framework'
  provides=("qt5-base=${pkgver}")
  conflicts=('qt5-base')
  install=${pkgname}.install

  cd ${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  install -D -m644 LICENSE* -t "$pkgdir"/usr/share/licenses/$pkgbase

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  # Fix wrong qmake path in pri file
  sed -i "s|${srcdir}/${_pkgfqn}|/usr|" \
    "${pkgdir}"/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/usr/bin/*; do
    ln -s /usr/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done
}

package_qt5-xcb-private-headers-ropieee() {
  pkgdesc='Private headers for Qt5 Xcb'
  depends=("qt5-base-raspberrypi=$pkgver")
  optdepends=()
  groups=()
  conflicts=()

  cd ${_pkgfqn}
  install -d -m755 "$pkgdir"/usr/include/qtxcb-private
  cp -r src/plugins/platforms/xcb/*.h "$pkgdir"/usr/include/qtxcb-private/
}
