# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=qt5-quickcontrols2
pkgname=qt5-quickcontrols2-ropieee
_qtver=5.12.7
pkgver=${_qtver/-/}
provides=("qt5-quickcontrols2=${pkgver}")
conflicts=('qt5-quickcontrols2')
pkgrel=1
arch=('armv7h')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Next generation user interface controls based on Qt Quick'
depends=('qt5-declarative')
optdepends=('qt5-graphicaleffects: for the Material style')
groups=('qt' 'qt5')
_pkgfqn="${pkgbase/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('3a9526e5ad01edbfb796a6631983c391ea1b7e22ae6e07840048156a9e92a237')

prepare() {
  mkdir -p build
}

build() {
  cd build

  echo 'QMAKE_LFLAGS_NOUNDEF=' >> ../${_pkgfqn}/.qmake.conf

  qmake -Wnone ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
